buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.danilopianini:build-commons:${buildCommonsVersion}"
    }
}

apply plugin: 'org.danilopianini.build-commons'

dependencies {
    compile "com.google.guava:guava:${guavaVersion}"
    compile "org.jgrapht:jgrapht-core:${jgraphtVersion}"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "junit:junit:${junitVersion}"
    doclet "org.jboss.apiviz:apiviz:${apivizVersion}"
}

project.task(type: Javadoc, overwrite: true, 'javadoc') {
    source project.configurations.doc.collect { zipTree(it) }
    def mainSourceSets = project.sourceSets.main
    source mainSourceSets.allJava
    classpath = mainSourceSets.output + mainSourceSets.compileClasspath
    include '**/*.java'
    destinationDir project.file("${project.buildDir}/docs/javadoc/")
    failOnError = false
    def longName = "${project.longName}"
    options {
        showAll()
        addBooleanOption('nopackagediagram', true)
        addStringOption('Xdoclint:none', '-quiet')
        windowTitle "${longName} version ${project.version} Javadoc API"
        docTitle "${longName} ${project.version} reference API"
        links 'http://docs.oracle.com/javase/8/docs/api/'
        links 'http://trove4j.sourceforge.net/javadocs/'
        doclet 'org.jboss.apiviz.APIviz'
        docletpath project.file(project.configurations.doclet.asPath)
    }
}

task(type: Jar, 'fatJar') {
    baseName = project.name + '-complete'
    from(project.configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with project.jar
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                developers {
                    developer {
                            name 'Danilo Pianini'
                            email 'danilo.pianini@gmail.com'
                            url 'http://www.danilopianini.org/'
                    }
                }
            }
        }
    }
}